::
::::  /hoon/merge/hood/gen
  ::
/?  314
!:
|%
++  beaky  {knot knot knot $~}
++  sorc  ?({bek/beaky $~} {her/@p sud/@tas $~})
--
::
::::
  !:
:-  %say
|=  $:  {now/@da eny/@uvI bek/beak}
        {arg/{?(sorc {syd/$@(desk beaky) sorc})} cas/case gem/?($auto germ)}
    ==
=*  our  p.bek
|^  :-  %kiln-merge
    ^-  {syd/desk her/ship sud/desk cas/case gem/?($auto germ)}
    ?-  arg
      {@ @ $~}
        =+(arg [sud ?.(=(our her) her (sein her)) sud (opt-case da+now) gem])
    ::
      {^ $~}
        =+  (pars bek.arg)
        [dez ?.(=(our who) who (sein who)) dez (opt-case caz) gem]
    ::
      {* @ @ $~}
        :-  (pars-src syd.arg)
        =+(arg [her sud (opt-case da+now) gem])
    ::
      {* ^ $~}
        :-  (pars-src syd.arg)
        =+((pars bek.arg) [who dez (opt-case caz) gem])]
    ==
++  opt-case  |=(a=case ?:(=(*case cas) a cas))  :: override
++  pars  |=(a/beaky `{{who/ship dez/desk caz/case} *}`(need (tome a)))
++  pars-src
  |=  syd/$@(desk beaky)
  ?@  syd  syd
  =+  (pars syd)
  ~|  [%into-foreign who `path`syd]
  ?>(=(our who) dez)
--
